%ifndef _ARRAY_
%def _ARRAY_


%import unsafe.alo
%import types.alo

class array<A>
{
    UnsafePointer<A> ptr;   // start
    int sz;                 // current size
    int cap;                // current capacity
    func _Desired_size_to_capacity(int _size) -> int
    {
        int u = floor(_size / 16);
        return (u + 1) * 16;
        // This is to be replcaced by the code below
        // since the binary operators are implemented:
        // return (_size >> 4) + 1 << 4;
    }
    func init(int _sz)
    {
        this->sz = _sz;
        this->cap = this->_Desired_size_to_capacity(_sz);
        this->ptr.malloc(this->cap);
        return;
    }
    func init()
    {
        this->sz = 0;
        this->cap = 16;
        this->ptr.malloc(this->cap);
        return;
    }
    func destroy()
    {
        this->ptr.free();
        this->sz = 0;
        this->cap = 0;
        return;
    }
    func size() -> int
    {
        return this->sz;
    }
    func capacity() -> int
    {
        return this->cap;
    }
    func data() -> A*
    {
        return this->ptr.load();
    }
    func udata() -> UnsafePointer<A>
    {
        return this->ptr;
    }
    func _Reallocate(int _Newsz)
    {
        this->cap = this->_Desired_size_to_capacity(_Newsz);
        UnsafePointer<A> newptr;
        newptr.malloc(this->cap);
        memcpy(this->ptr.addr, newptr.addr, this->sz * sizeof(A));
        this->ptr.free();
        this->ptr = newptr;
        return;
    }
    func concat(A* _ptr, int _sz)
    {
        int newsz = this->sz + _sz;
        if(newsz > this->cap) this->_Reallocate(newsz);
        UnsafePointer<A> _ptr_wrapper;
        _ptr_wrapper.toObj(_ptr);
        memcpy(_ptr_wrapper.addr, this->ptr.addr + this->sz * sizeof(A), _sz * sizeof(A));
        this->sz = newsz;
        return;
    }
    func append(A obj)
    {
        this->concat(&obj, 1);
        return;
    }
    func push_back(A obj)
    {
        this->concat(&obj, 1);
        return;
    }
    func get(int _Index) -> A*
    {
        UnsafePointer<A> cur;
    	cur.addr=this->ptr.addr+_Index * sizeof(A);
        return cur.load();
    }
    func pop_back() -> A
    {
        // TODO:
        // if(this->sz < 1) throw some error;
        UnsafePointer<A> p;
        p.addr = this->ptr.addr;
        p.addr = p.addr + this->sz * sizeof(A) - sizeof(A);
        this->sz = this->sz - 1;
        return *(p.load());
    }
    func erase_back(int _Size_to_erase)
    {
        // array = [0, 1, 2, 3, 4, 5, 6]
        // array.erase_back(3)
        // array = [0, 1, 2, 3]
        this->sz = this->sz - _Size_to_erase;
        return;
    }
    func back() -> A
    {
        // if(this->sz < 1) throw some error;
        UnsafePointer<A> p;
        p.addr = this->ptr.addr;
        p.addr = p.addr + this->sz * sizeof(A) - sizeof(A);
        return *(p.load());
    }
}

%endif